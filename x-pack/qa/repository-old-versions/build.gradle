/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0; you may not use this file except in compliance with the Elastic License
 * 2.0.
 */

import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin:  org.elasticsearch.gradle.internal.test.StandaloneRestTestPlugin
apply plugin: 'elasticsearch.rest-resources'

configurations {
  oldesFixture
}

dependencies {
  testImplementation project(':test:test-clusters')
  testImplementation project(':test:fixtures:old-elasticsearch')
}

restResources {
  restApi {
    include '_common', 'search'
  }
  restTests {
    includeCore 'search/390_doc_values_search.yml'
  }
}

int currentMajorVersion = org.elasticsearch.gradle.VersionProperties.elasticsearchVersion.major
assert (currentMajorVersion - 2) == 7: "add archive BWC tests for major version " + (currentMajorVersion - 2)
//  for (String versionString : ['5.0.0', '5.6.16', '6.0.0', '6.8.20']) {
for (String versionString : ['6.8.20']) {
  Version version = Version.fromString(versionString)
  String versionNoDots = version.toString().replace('.', '_')

  tasks.register("javaRestTest${versionNoDots}", StandaloneRestIntegTestTask).configure {
    usesDefaultDistribution()
    systemProperty "tests.es.version", version.toString()
    maxParallelForks = 1
  }
}
